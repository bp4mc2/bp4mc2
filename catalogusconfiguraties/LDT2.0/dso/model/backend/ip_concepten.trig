# InformationProduct Concepten

GRAPH catalogus: {
	catalogus:Concept a elmo:InformationProduct;
		elmo:backend catalogus:MyLocalSparqlBackend;
		elmo:query '''
			prefix skos: <http://www.w3.org/2004/02/skos/core#>
			prefix dct: <http://purl.org/dc/terms/>
			prefix thes: <http://purl.org/iso25964/skos-thes#>
			prefix prov: <http://www.w3.org/ns/prov#>
			prefix dc: <http://purl.org/dc/terms/>
			prefix skoslex: <http://bp4mc2.org/def/skos-lex/>
			prefix container: <http://localhost:8080/catalogus/dso/container/>
			prefix doc: <http://localhost:8080/catalogus/dso/concepten/doc/>
			prefix dataset: <http://localhost:8080/catalogus/dso/id/dataset/>
			prefix concept: <http://localhost:8080/catalogus/dso/id/concept/>
			prefix brt: <http://brt.basisregistraties.overheid.nl/id/dataset/>
			prefix bp4mc2: <http://bp4mc2.org/elmo/def/>
			prefix dcat: <http://www.w3.org/ns/dcat#>

			CONSTRUCT {
				<@IDSUBJECT@> ?p ?o.
				?o rdfs:label ?olabel.
				?o rdfs:label ?oelabel.
				<@IDSUBJECT@> skos:member ?col.
				?col rdfs:label ?col_label.
				<@IDSUBJECT@> prov:wasGeneratedBy ?a.
				<@IDSUBJECT@> prov:generatedAtTime ?sStartTime.
				?a rdfs:label ?alabel.
				?c rdfs:label ?clabel.
				<@IDSUBJECT@> skos:closeMatch ?toe_label.
				
			}
			WHERE
			{
				{
					GRAPH ?g {
						<@IDSUBJECT@> ?p ?o.
						<@IDSUBJECT@> rdf:type skos:Concept.
						?g prov:wasGeneratedBy ?a.
						?g prov:generatedAtTime ?sStartTime.
						
						OPTIONAL {
							<@IDSUBJECT@> prov:invalidatedAtTime ?sEndTime.
						}
						OPTIONAL {
							?a rdfs:label ?alabel.
						}
						FILTER (
							strdt( ?sStartTime, xsd:dateTime ) <= "@TIMESTAMP@"^^xsd:dateTime
							&& (
								!bound( ?sEndTime )
								|| strdt( ?sEndTime, xsd:dateTime ) > "@TIMESTAMP@"^^xsd:dateTime
							)
						)
						FILTER (
							?p != rdf:type
							&& ?p != bp4mc2:signature
							&& ?p != dct:type
							&& ?p != skos:member
						)
					}
				}
				OPTIONAL {
					GRAPH ?og {
						?o rdfs:label ?olabel.
						?og prov:generatedAtTime ?oStartTime.
						OPTIONAL {
							?o prov:invalidatedAtTime ?oEndTime.
						}
						FILTER (
							strdt( ?oStartTime, xsd:dateTime ) <= "@TIMESTAMP@"^^xsd:dateTime
							&& (
								!bound( ?oEndTime )
								|| strdt( ?oEndTime, xsd:dateTime ) > "@TIMESTAMP@"^^xsd:dateTime
							)
						)	
					}
				}
				OPTIONAL {
					GRAPH ?cg {
						?col skos:member <@IDSUBJECT@>.
						OPTIONAL {
							?col rdfs:label ?col_label.
						}
						?cg prov:generatedAtTime ?cStartTime.
						OPTIONAL {
							?col prov:invalidatedAtTime ?cEndTime.
						}
						FILTER (
							strdt( ?cStartTime, xsd:dateTime ) <= "@TIMESTAMP@"^^xsd:dateTime
							&& (
								!bound( ?cEndTime )
								|| strdt( ?cEndTime, xsd:dateTime ) > "@TIMESTAMP@"^^xsd:dateTime
							)
						)	
					}
				}
				OPTIONAL {
					GRAPH ?tg {
						?toe skos:closeMatch <@IDSUBJECT@>.
						?toe rdfs:label ?toe_label.
						?tg prov:generatedAtTime ?tgStartTime.
						
						OPTIONAL {
							?toe prov:invalidatedAtTime ?tgEndTime.
						}
						FILTER (
							strdt( ?tgStartTime, xsd:dateTime ) <= "@TIMESTAMP@"^^xsd:dateTime
							&& (
								!bound( ?tgEndTime )
								|| strdt( ?tgEndTime, xsd:dateTime ) > "@TIMESTAMP@"^^xsd:dateTime
							)
						)	
					}
				}
			}
		'''
	.
	
	catalogus:Concepten a elmo:InformationProduct;
		elmo:backend catalogus:MyLocalSparqlBackend;
		elmo:query '''
			prefix prov: <http://www.w3.org/ns/prov#>
			prefix skoslex: <http://bp4mc2.org/def/skos-lex/>
			prefix container: <http://localhost:8080/catalogus/dso/container/>
			prefix doc: <http://localhost:8080/catalogus/dso/concepten/doc/>
			prefix collection: <http://localhost:8080/catalogus/dso/id/collection/>
			prefix concept: <http://localhost:8080/catalogus/dso/id/concept/>
			
			SELECT ?concept ?concept_label ?uitleg
			WHERE {
				GRAPH ?g {
					?concept rdf:type skos:Concept.
					?concept rdfs:label ?concept_label.
					OPTIONAL {?concept skos:definition ?uitleg}
					FILTER NOT EXISTS {
						?concept prov:invalidatedAtTime ?time.
					}
				}
				GRAPH doc:mastergraph {
					?concept rdfs:isDefinedBy ?g
				}
				FILTER NOT EXISTS {
					GRAPH ?toeleidingsGraph {
						collection:Toeleidingsbegrippen skos:member ?concept.
						FILTER NOT EXISTS {
							collection:Toeleidingsbegrippen prov:invalidatedAtTime ?ctime.
						}
					}
					GRAPH doc:mastergraph {
						collection:Toeleidingsbegrippen rdfs:isDefinedBy ?toeleidingsGraph.
					}
				}
			}
			ORDER BY lcase(?concept_label)
		'''
	.
}